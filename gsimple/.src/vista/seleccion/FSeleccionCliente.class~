' Gambas class file

Property Read Cliente As CCliente 
Private m_Cliente As CCliente

Public Function Seleccionar() As Boolean
  Me.Persistent = True
  Return Me.ShowModal()
End

Public Sub Form_Open()
  InicarGrid
  CargarLista
End

Private Sub InicarGrid()
  With grd
      .Columns.Count = 2
  End With
  
  'Codigo
  With grd.Columns[0]
    .Text = "CÃ³digo"
    .Alignment = Align.Right
    .Width = 80
  End With
  
  'Nombre
  With grd.Columns[1]
    .Text = "Nombre"
    .Alignment = Align.Left
    .Width = 150
  End With
End

Private Sub CargarLista()
  Dim Cliente As CCliente
  Dim Clientes As CCliente[]
  Dim Filtro As String 
  Dim i As Integer = 0

  If txtFiltro.Length > 0 Then
    Filtro = "nombre LIKE " & MData.GetSQLText(txtFiltro.Text & "%")
  Endif
  
  Clientes = MCliente.GetAll(Filtro)
  
  grd.Rows.Count = Clientes.Count
  
  For Each Cliente In Clientes
    With Cliente
      grd[i, 0].Text = .Id
      grd[i, 1].Text = .Nombre
    End With
    i += 1
  Next

  If Clientes.Count = 0 And txtFiltro.Length > 0 Then
    txtFiltro.Background = &HFFBFBF
  Else
    txtFiltro.Background = Color.TextBackground
  Endif
End

Public Sub grd_DblClick()
  cmdOk.Value = True
End

Public Sub grd_KeyPress()
  Select Case Key.Code
    Case Key.Enter
      cmdOk.Value = True
  End Select
End

Public Sub txtFiltro_KeyPress()
  If Key.Code = Key.Enter Then
    CargarLista()
  Endif
End

Public Sub cmdOk_Click()
  If grd.Row >= 0 Then
    m_Cliente = MCliente.GetById(grd[grd.Row, 0].Text)
    Me.Close(True)
  Endif
End

Public Sub cmdCancelar_Click()
  m_Cliente = Null
  Me.Close(False)
End

Private Function Cliente_Read() As CCliente
  Return m_Cliente
End
